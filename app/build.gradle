buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "xyz.imaginatrix.synapse"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 4
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false

            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
        
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


ext {
    supportLibVersion = '27.0.2'
    butterknifeVersion = '8.8.1'
    retrofit2Version = '2.3.0'
    okHttpVersion = '3.8.0'
    fastAdapterVersion = '3.0.3@aar'
}

configurations {
    compile.exclude group: 'xpp3'
    compile.exclude module: 'stax-api'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"

    // View dependency libs
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // Network libs
    implementation("com.squareup.retrofit2:retrofit:$retrofit2Version") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-simplexml:$retrofit2Version"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Reactive libs
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // FastAdapter
    implementation("com.mikepenz:fastadapter:$fastAdapterVersion") { transitive = true }
    implementation "com.mikepenz:fastadapter-commons:$fastAdapterVersion"
    implementation "com.mikepenz:fastadapter-extensions:$fastAdapterVersion"
    implementation 'com.mikepenz:materialize:1.0.3@aar'

    // Miscellaneous
    implementation 'com.github.doctoror.particlesdrawable:library:1.0.8'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.1'
    implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    implementation 'com.mikepenz:community-material-typeface:1.7.22.1@aar'
    implementation "com.mikepenz:iconics-core:2.8.2@aar"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') { transitive = true }
}
